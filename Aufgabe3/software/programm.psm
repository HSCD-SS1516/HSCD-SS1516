
        CONSTANT IO_Port,  00
        CONSTANT LED0,     01
        CONSTANT LED1,     02
        CONSTANT BNT0,     04
        
        CONSTANT COM_Port, 01
        CONSTANT TXD,      01
        CONSTANT RXD,      01

        CONSTANT RAM_A0_Port, 02
        CONSTANT RAM_A1_Port, 03
        CONSTANT RAM_D_Port,  04

        CONSTANT CPROC_STATUS_Port, 08
        CONSTANT CPROC_CTRL_Port, 08
        CONSTANT CPROC_PTR0_Port, 09
        CONSTANT CPROC_PTR1_Port, 0A
        CONSTANT CPROC_LEN_Port, 0B

        CONSTANT TXDIDLE,  00
        CONSTANT TXDSTART, 01
        CONSTANT TXDWORK,  02
        CONSTANT TXDSTOP,  03
        CONSTANT RXDIDLE,  00
        CONSTANT RXDSTART, 01
        CONSTANT RXDWORK,  02
        CONSTANT WAIT_CPROC, 03

        CONSTANT ASCII0,   30
        CONSTANT ASCII9,   3A        ; '9' + 1
        CONSTANT ASCIIA,   41
        CONSTANT ASCIIZ,   5B        ; 'Z' + 1
        CONSTANT ASCIIa,   61
        CONSTANT ASCIIz,   7B        ; 'z' + 1

        CONSTANT BYTECNT_RXD_ADDR, 00
        CONSTANT BYTECNT_TXD_ADDR, 01
        CONSTANT VALIDBYTES_ADDR, 02
        CONSTANT ADDRH_RXD_ADDR, 03  ; Address for ADDRX_RXD: (00 & ADDRX_RXD_ADDR)
        CONSTANT ADDRL_RXD_ADDR, 04
        CONSTANT ADDRH_TXD_ADDR, 05  ; Address for ADDRX_RXD: (00 & ADDRX_TXD_ADDR)
        CONSTANT ADDRL_TXD_ADDR, 06

        CONSTANT BLOCKLEN, FF

; Register Mapping
; alle acht Register s0 bis s7 stehen der ISR zur Verfuegung
        ; Temporary variables
        NAMEREG s0, ADDRL
        NAMEREG s1, ADDRH
        NAMEREG s2, TMP
        NAMEREG s3, IOR
        NAMEREG s4, BYTECNT
        ; BLINK variables
        NAMEREG s5, CNTH
        NAMEREG s6, CNTL
        ; RXD variables
        NAMEREG s7, RXDSTATE
        NAMEREG s8, RXDCNTR
        NAMEREG s9, SAVEDBITS
        NAMEREG sA, READBYTE
        ; TXD variables
        NAMEREG sB, TXDSTATE
        NAMEREG sC, TXDCNTR
        NAMEREG sD, SENTBITS
        NAMEREG sE, SENDBUF
        ; Shared variables
        NAMEREG sF, AVAILABLE_BYTES

; alle acht Register s8 bis sF stehen der Main-Funktion zur Verfuegung

; --------------------------------------------------------------------------------------------

        ADDRESS 000
; Hauptfunktion
start:  AND    CNTH, 00   ; Initialisierung der Zaehlers ...
        AND    CNTL, 00   ; in der ISR
        LOAD RXDCNTR, 00
        LOAD TXDCNTR, 00
        LOAD AVAILABLE_BYTES, 00
        ; Initialize ADDR_RXD in RAM (01 & 00)
        ; BEGIN
        LOAD   TMP,   00
        OUTPUT TMP,   RAM_A1_Port
        LOAD   TMP,   ADDRL_RXD_ADDR
        OUTPUT TMP,   RAM_A0_Port
        LOAD   TMP,   00
        OUTPUT TMP,   RAM_D_Port
        LOAD   TMP,   00
        OUTPUT TMP,   RAM_A1_Port
        LOAD   TMP,   ADDRH_RXD_ADDR
        OUTPUT TMP,   RAM_A0_Port
        LOAD   TMP,   01
        OUTPUT TMP,   RAM_D_Port
        ; END
        ; Initialize BYTECNT_RXD to 00
        ; BEGIN
        LOAD TMP, BYTECNT_RXD_ADDR
        OUTPUT TMP, RAM_A0_Port
        LOAD TMP, 00
        OUTPUT TMP, RAM_A1_Port
        OUTPUT TMP, RAM_D_Port
        ; END
        ENABLE INTERRUPT
loop:   ; do nothing
        JUMP loop

; --------------------------------------------------------------------------------------------
 
; Subroutine, die den Zustand des Buttons BTN0 auf die Leuchtdiode LED0 durchschaltet
BNT0LED0:
        INPUT  IOR,  IO_Port ; IO-Port einlesen
        INPUT  TMP,  IO_Port ; IO-Port einlesen
        AND    TMP,  BNT0    ; Zustand von BNT0 ermitteln
        SR0    TMP           ; auf die Position ...
        SR0    TMP           ; ... von LED0 verschieben
        AND    IOR,  LED1    ; Zustand von LED1 ermitteln
        OR     IOR,  TMP     ; LED1 mit BNT0 konkatinieren
        OUTPUT IOR,  IO_Port ; den neuen Wert ueber IO-Port ausgeben 
        RETURN

; --------------------------------------------------------------------------------------------

; Subroutine, die die Leuchtdione LED1 mit ca. 1 Hz blinken laesst
BLINK:  ADD    CNTL, 01      ; den 16-Bit-Zaehler ... 
        ADDCY  CNTH, 00      ; .. inkrementieren
        JUMP   NC,   BLINK0  ; kein Ueberlauf -> exit, sonst
        INPUT  IOR,  IO_Port ; IO-Port einlesen
        XOR    IOR,  LED1    ; LED1 toggeln
        OUTPUT IOR,  IO_Port ; den neuen Wert ueber IO-Port ausgeben 
BLINK0: RETURN

; --------------------------------------------------------------------------------------------

; Subroutine that handles UART data reception
COMM_RECEIVE:
        COMPARE RXDSTATE, RXDSTART
        JUMP Z, RXD_START
        COMPARE RXDSTATE, RXDWORK
        JUMP Z, RXD_WORK
        COMPARE RXDSTATE, WAIT_CPROC
        JUMP Z, WAITCPROC
RXD_IDLE:
        INPUT IOR, COM_Port             ; Read data from COM port
        AND IOR, RXD
        COMPARE IOR, 00
        JUMP NZ, RXD_RET
        ; If data is equal to 0b0 (start bit), go to start state
        LOAD RXDSTATE, RXDSTART
        JUMP RXD_RET
RXD_START:
        SUB RXDCNTR, 20
        JUMP NZ, RXD_RET                ; Wait for 8 clock periods (RXDCNTR underflow)
        INPUT IOR, COM_Port             ; Read data from COM port
        AND IOR, RXD
        LOAD RXDSTATE, RXDIDLE          ; If data is different to 0b0 (start bit), return to idle state
        COMPARE IOR, 00
        JUMP NZ, RXD_RET
        LOAD RXDSTATE, RXDWORK          ; If data is equal to 0b0 (start bit), go to work state
        LOAD READBYTE, 00
        JUMP RXD_RET
RXD_WORK:
        SUB RXDCNTR, 10
        JUMP NZ, RXD_RET                ; Wait for 16 clock periods (RXDCNTR underflow)
        INPUT IOR, COM_Port             ; Read data from COM port
        AND IOR, RXD                    ; Keep only the LSB
        OR READBYTE, IOR                ; Copy read bit into READBYTE's LSB
        RR READBYTE                     ; Rotate READBYTE to the right and put LSB in MSB
        SUB SAVEDBITS, 20
        JUMP NZ, RXD_RET                ; If all 8 bits have been read continue with check
        ; Load BYTECNT_RXD (00..0 & BYTECNT_RXD_ADDR) from RAM into BYTECNT
        ; BEGIN
        LOAD TMP, 00
        OUTPUT TMP, RAM_A1_Port
        LOAD TMP, BYTECNT_RXD_ADDR
        OUTPUT TMP, RAM_A0_Port
        INPUT BYTECNT, RAM_D_Port
        ; END
        ADD BYTECNT, 01                 ; Increment BYTECNT_RXD
        OUTPUT BYTECNT, RAM_D_Port      ; Store BYTECNT_RXD back in RAM
        LOAD RXDSTATE, RXDIDLE          ; Go to reception idle state
CHAR_CHECK:
        ; If READBYTE is an ASCII number or letter, go to CHAR_VALID.
        ; Otherwise, go to CHAR_INVALID
        COMPARE READBYTE, ASCII0
        JUMP C, CHAR_INVALID
        COMPARE READBYTE, ASCII9
        JUMP C, CHAR_VALID
        COMPARE READBYTE, ASCIIA
        JUMP C, CHAR_INVALID
        COMPARE READBYTE, ASCIIZ
        JUMP C, CHAR_VALID
        COMPARE READBYTE, ASCIIa
        JUMP C, CHAR_INVALID
        COMPARE READBYTE, ASCIIz
        JUMP NC, CHAR_INVALID
CHAR_VALID:
        ; Load VALIDBYTES
        ; BEGIN
        LOAD TMP, 00
        OUTPUT TMP, RAM_A1_Port
        LOAD TMP, VALIDBYTES_ADDR
        OUTPUT TMP, RAM_A0_Port
        INPUT TMP, RAM_D_Port
        ; END
        ADD TMP, 01                     ; Increment VALIDBYTES
        OUTPUT TMP, RAM_D_Port          ; Store back
STORE_CHAR:
        ; Store read byte in RAM and increment current address for RXD
        ; BEGIN
        ;       Load ADDR_RXD (00..0 & ADDR_RXD_ADDR) from RAM
        ;       BEGIN
        LOAD TMP, 00
        OUTPUT TMP, RAM_A1_Port
        LOAD TMP, ADDRH_RXD_ADDR
        OUTPUT TMP, RAM_A0_Port
        INPUT ADDRH, RAM_D_Port
        LOAD TMP, ADDRL_RXD_ADDR
        OUTPUT TMP, RAM_A0_Port
        INPUT ADDRL, RAM_D_Port
        ;       END
        OUTPUT ADDRL, RAM_A0_Port
        OUTPUT ADDRH, RAM_A1_Port
        OUTPUT READBYTE, RAM_D_Port
        ADD ADDRL, 01
        JUMP NC, STORE_ADDR_RXD
        ADD ADDRH, 01
        JUMP NZ, STORE_ADDR_RXD         ; Check for overflow
        ADD ADDRH, 01
STORE_ADDR_RXD:
        ;       Store ADDR_RXD variables back in RAM
        ;       BEGIN
        LOAD TMP, 00
        OUTPUT TMP, RAM_A1_Port
        LOAD TMP, ADDRH_RXD_ADDR
        OUTPUT TMP, RAM_A0_Port
        OUTPUT ADDRH, RAM_D_Port
        LOAD TMP, ADDRL_RXD_ADDR
        OUTPUT TMP, RAM_A0_Port
        OUTPUT ADDRL, RAM_D_Port
        ;       END
        ; END
CHAR_INVALID:
        ; Load BYTECNT_RXD (00..0 & BYTECNT_RXD_ADDR) from RAM into BYTECNT
        ; BEGIN
        LOAD TMP, 00
        OUTPUT TMP, RAM_A1_Port
        LOAD TMP, BYTECNT_RXD_ADDR
        OUTPUT TMP, RAM_A0_Port
        INPUT BYTECNT, RAM_D_Port
        ; END
        COMPARE BYTECNT, BLOCKLEN
        JUMP NZ, RXD_RET
        ; Set the PTR value to ADDR_RXD - VALIDBYTES
        ; BEGIN
        ;       Load ADDR_RXD (00..0 & ADDR_RXD_ADDR) from RAM
        ;       BEGIN
        LOAD TMP, 00
        OUTPUT TMP, RAM_A1_Port
        LOAD TMP, ADDRH_RXD_ADDR
        OUTPUT TMP, RAM_A0_Port
        INPUT ADDRH, RAM_D_Port
        LOAD TMP, ADDRL_RXD_ADDR
        OUTPUT TMP, RAM_A0_Port
        INPUT ADDRL, RAM_D_Port
        ;       END
        ;       Load VALIDBYTES
        ;       BEGIN
        LOAD TMP, 00
        OUTPUT TMP, RAM_A1_Port
        LOAD TMP, VALIDBYTES_ADDR
        OUTPUT TMP, RAM_A0_Port
        INPUT TMP, RAM_D_Port
        ;       END
        SUB ADDRL, TMP
        JUMP NC, SETPTR
        SUB ADDRH, 01
SETPTR:
        OUTPUT ADDRL, CPROC_PTR0_Port
        OUTPUT ADDRH, CPROC_PTR1_Port
        ; END
        ; Set the LEN value to VALIDBYTES
        ; BEGIN
        OUTPUT TMP, CPROC_LEN_Port
        ; END
        ; Start the sorting in the coprocessor
        ; BEGIN
        LOAD TMP, 01
        OUTPUT TMP, CPROC_CTRL_Port
        ; END
        LOAD RXDSTATE, WAIT_CPROC
        JUMP RXD_RET
WAITCPROC:
        ; Set the strt flag to 0
        ; BEGIN
        LOAD TMP, 00
        OUTPUT TMP, CPROC_CTRL_Port
        ; END
        INPUT TMP, CPROC_STATUS_Port
        COMPARE TMP, 01
        JUMP NZ, RXD_RET
        ; Load VALIDBYTES
        ; BEGIN
        LOAD TMP, 00
        OUTPUT TMP, RAM_A1_Port
        LOAD TMP, VALIDBYTES_ADDR
        OUTPUT TMP, RAM_A0_Port
        INPUT TMP, RAM_D_Port
        ; END
        ADD AVAILABLE_BYTES, TMP        ; Tell the TXD process to send VALIDBYTES more bytes
        LOAD TMP, 00                    ; Reset VALIDBYTES
        OUTPUT TMP, RAM_D_Port          ; Store back
        ; Reset BYTECNT_RXD
        ; BEGIN
        LOAD TMP, BYTECNT_RXD_ADDR
        OUTPUT TMP, RAM_A0_Port
        LOAD TMP, 00
        OUTPUT TMP, RAM_A1_Port
        OUTPUT TMP, RAM_D_Port
        ; END
        LOAD RXDSTATE, RXDIDLE
RXD_RET:
        RETURN

; --------------------------------------------------------------------------------------------

; Subroutine that handles UART data sending
COMM_SEND:
        COMPARE TXDSTATE, TXDSTART
        JUMP Z, TXD_START
        COMPARE TXDSTATE, TXDWORK
        JUMP Z, TXD_WORK
        COMPARE TXDSTATE, TXDSTOP
        JUMP Z, TXD_STOP
TXD_IDLE:
        LOAD TMP, 01
        OUTPUT TMP, TXD                 ; Output 0b1
        COMPARE AVAILABLE_BYTES, 00
        JUMP Z, TXD_RET                 ; If there are no new bytes available stay in TXD_IDLE
        ; Wait for 16 clock periods to start transmitting
        SUB TXDCNTR, 10
        JUMP NZ, TXD_RET
        ; Setup TXD process data
        ; BEGIN
        LOAD TXDSTATE, TXDSTART         ; Go to transmission start state
        ;       Initialize BYTECNT_TXD to AVAILABLE_BYTES
        ;       BEGIN
        LOAD TMP, 00
        OUTPUT TMP, RAM_A1_Port
        LOAD TMP, BYTECNT_TXD_ADDR
        OUTPUT TMP, RAM_A0_Port
        LOAD TMP, AVAILABLE_BYTES
        OUTPUT TMP, RAM_D_Port
        ;       END
        ;       Initialize ADDR_TXD_ADDR (00..0 & BYTECNT_TXD_ADDR) to ADDR_RXD_ADDR
        ;       BEGIN
        LOAD TMP, 00
        OUTPUT TMP, RAM_A1_Port
        LOAD TMP, ADDRH_RXD_ADDR
        OUTPUT TMP, RAM_A0_Port
        INPUT ADDRH, RAM_D_Port
        LOAD TMP, ADDRL_RXD_ADDR
        OUTPUT TMP, RAM_A0_Port
        INPUT ADDRL, RAM_D_Port
        LOAD TMP, ADDRH_TXD_ADDR
        OUTPUT TMP, RAM_A0_Port
        OUTPUT ADDRH, RAM_D_Port
        LOAD TMP, ADDRL_TXD_ADDR
        OUTPUT TMP, RAM_A0_Port
        OUTPUT ADDRL, RAM_D_Port
        ;       END
        ; END
TXD_START:
        LOAD TMP, 00
        OUTPUT TMP, TXD                 ; Output 0b0 (start bit)
        SUB TXDCNTR, 10
        JUMP NZ, TXD_RET                ; Repeat for 16 clock periods (TXDCNTR underflow)
        ; Retrieve BYTECNT_TXD (00..0 & BYTECNT_TXD_ADDR) from RAM into BYTECNT
        ; BEGIN
        LOAD TMP, 00
        OUTPUT TMP, RAM_A1_Port
        LOAD TMP, BYTECNT_TXD_ADDR
        OUTPUT TMP, RAM_A0_Port
        INPUT BYTECNT, RAM_D_Port
        ; END
        ;       Load address (ADDRH & ADDRL) - BYTECNT_TXD into SENDBUF
        ;       BEGIN
        ;               Load ADDR_TXD (00..0 & ADDR_TXD_ADDR) from RAM
        ;               BEGIN
        LOAD TMP, 00
        OUTPUT TMP, RAM_A1_Port
        LOAD TMP, ADDRH_TXD_ADDR
        OUTPUT TMP, RAM_A0_Port
        INPUT ADDRH, RAM_D_Port
        LOAD TMP, ADDRL_TXD_ADDR
        OUTPUT TMP, RAM_A0_Port
        INPUT ADDRL, RAM_D_Port
        ;               END
        LOAD TMP, ADDRL
        SUB TMP, BYTECNT
        OUTPUT TMP, RAM_A0_Port
        OUTPUT ADDRH, RAM_A1_Port
        JUMP NC, LOAD_CHAR
        LOAD TMP, ADDRH
        SUB TMP, 01
        OUTPUT TMP, RAM_A1_Port
        ;       END
LOAD_CHAR:
        INPUT SENDBUF, RAM_D_Port
        ; END
        LOAD TXDSTATE, TXDWORK          ; Go to work state
TXD_WORK:
        OUTPUT SENDBUF, TXD             ; Write SENDBUF into TXD register
        SUB TXDCNTR, 10
        JUMP NZ, TXD_RET                ; Repeat for 16 clock periods (TXDCNTR underflow)
        SR0 SENDBUF                     ; Rotate SENDBUF to the right (send from LSB to MSB)
        SUB SENTBITS, 20                ; Repeat all above for each bit in SENDBUF
        JUMP Z, TXD_WORK_CHANGESTATE
        JUMP TXD_RET
TXD_WORK_CHANGESTATE:
        LOAD TXDSTATE, TXDSTOP          ; If all 8 bits have been sent, go to stop state
TXD_STOP:
        LOAD TMP, 01
        OUTPUT TMP, TXD                 ; Output 0b1 (stop bit)
        SUB TXDCNTR, 10
        JUMP NZ, TXD_RET                ; Repeat for 16 clock periods (TXDCNTR underflow)
        ; Decrement BYTECNT_TXD and AVAILABLE_BYTES
        ; BEGIN
        ;       Load BYTECNT_TXD (00..0 & BYTECNT_TXD_ADDR) from RAM into BYTECNT
        ;       BEGIN
        LOAD TMP, 00
        OUTPUT TMP, RAM_A1_Port
        LOAD TMP, BYTECNT_TXD_ADDR
        OUTPUT TMP, RAM_A0_Port
        INPUT BYTECNT, RAM_D_Port
        ;       END
        SUB AVAILABLE_BYTES, 01
        SUB BYTECNT, 01                 ; Decrement BYTECNT_TXD
        OUTPUT BYTECNT, RAM_D_Port      ; Store BYTECNT_TXD back in RAM
        ; END
        ; If BYTECNT_TXD > 0, remain in start state
        LOAD TXDSTATE, TXDSTART
        JUMP NZ, TXD_RET
        LOAD TXDSTATE, TXDIDLE
TXD_RET:
        RETURN

; --------------------------------------------------------------------------------------------
        
        ADDRESS 300
; Interrupt-Service-Routine
ISR:    CALL    BNT0LED0
        CALL    BLINK
        CALL    COMM_RECEIVE
        CALL    COMM_SEND
        RETURNI ENABLE

; --------------------------------------------------------------------------------------------

; Interrupt-Vektor
        ADDRESS 3FF
        JUMP ISR
